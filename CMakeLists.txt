CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(HIPACC)
OPTION(USE_POLLY "Use Polly for analysis" OFF) 
INCLUDE(CMakeDependentOption)
CMAKE_DEPENDENT_OPTION(USE_JIT_ESTIMATE "Compile kernels JIT to estimate resource usage" ON "NOT APPLE" OFF)

#SET(CMAKE_VERBOSE_MAKEFILE on)

SET(HIPACC_MAJOR_VERSION 0)
SET(HIPACC_MINOR_VERSION 23)
SET(HIPACC_PATCH_VERSION 42)
SET(HIPACC_VERSION ${HIPACC_MAJOR_VERSION}.${HIPACC_MINOR_VERSION}.${HIPACC_PATCH_VERSION})
SET(HIPACC_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
SET(HIPACC_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR})

# add path for custom modules
SET(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake/modules)
FIND_PACKAGE(LLVM REQUIRED)
INCLUDE_DIRECTORIES(${LLVM_INCLUDE_DIR})

FIND_PACKAGE(CUDA)
FIND_PACKAGE(OpenCL)

IF(NOT CUDA_FOUND AND NOT OPENCL_FOUND)
    MESSAGE(FATAL_ERROR "Neither CUDA, nor OpenCL found. At least one of them is required to use HIPACC properly.")
ENDIF(NOT CUDA_FOUND AND NOT OPENCL_FOUND)

IF(CUDA_FOUND)
    SET(NVCC_COMPILER "${CUDA_TOOLKIT_ROOT_DIR}/bin/nvcc")
ENDIF(CUDA_FOUND)

IF(OPENCL_FOUND)
    SET(OCL_COMPILER "${CMAKE_INSTALL_PREFIX}/bin/ocl_compile")
ENDIF(OPENCL_FOUND)

MESSAGE(STATUS "Configuration summary:")
MESSAGE(STATUS "===")
MESSAGE(STATUS "USE_POLLY=${USE_POLLY}")
MESSAGE(STATUS "USE_JIT_ESTIMATE=${USE_JIT_ESTIMATE}")
MESSAGE(STATUS "===")


SET(DSL_INCLUDES "${CMAKE_INSTALL_PREFIX}/include")

SET(CMAKE_CXX_FLAGS "-Wall -Wunused ${LLVM_CXXFLAGS}")
#ADD_DEFINITIONS(-DTEST_DEFINITION)


# platform specific fixes
IF(APPLE)
    IF("${DARWIN_MAJOR_VERSION}" LESS 11)
        SET(PLATFORM_FIXES "-U __STRICT_ANSI__ -D NO_BOOST")
    ELSE("${DARWIN_MAJOR_VERSION}" LESS 11)
        SET(PLATFORM_FIXES "-D NO_BOOST")
    ENDIF("${DARWIN_MAJOR_VERSION}" LESS 11)
ELSE(APPLE)
    SET(PLATFORM_FIXES "-D NO_BOOST")
ENDIF(APPLE)

INCLUDE_DIRECTORIES(
    ${CMAKE_CURRENT_SOURCE_DIR}/include
    ${CMAKE_CURRENT_SOURCE_DIR}/lib)


#add_subdirectory(include)
ADD_SUBDIRECTORY(lib)
ADD_SUBDIRECTORY(compiler)
ADD_SUBDIRECTORY(tools)


# configure a header file to pass some of the CMake settings to the source code
CONFIGURE_FILE(
    "${HIPACC_SOURCE_DIR}/include/hipacc/Config/config.h.cmake"
    "${HIPACC_SOURCE_DIR}/include/hipacc/Config/config.h")
CONFIGURE_FILE(
    "${HIPACC_SOURCE_DIR}/tests/Makefile.cmake"
    "${HIPACC_SOURCE_DIR}/tests/Makefile")
CONFIGURE_FILE(
    "${HIPACC_SOURCE_DIR}/tests/Makefile_CL.cmake"
    "${HIPACC_SOURCE_DIR}/tests/Makefile_CL")


FILE(GLOB dsl_headers "${CMAKE_CURRENT_SOURCE_DIR}/dsl/*.hpp")
INSTALL(FILES ${dsl_headers} DESTINATION include)

INSTALL(DIRECTORY tests
    DESTINATION ${CMAKE_INSTALL_PREFIX}
    PATTERN ".svn" EXCLUDE
    PATTERN "tests/Makefile.cmake" EXCLUDE
    PATTERN "tests/Makefile" EXCLUDE
    PATTERN "tests/Makefile_CL.cmake" EXCLUDE
    PATTERN "tests/Makefile_CL" EXCLUDE
    PATTERN "tests/benchmark.sh" PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_WRITE GROUP_EXECUTE WORLD_READ)

INSTALL(FILES tests/Makefile
    DESTINATION ${CMAKE_INSTALL_PREFIX})

INSTALL(FILES tests/Makefile_CL
    DESTINATION ${CMAKE_INSTALL_PREFIX})

# TODO: build and install documentation (pdf)

